/**
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath 'org.grails.plugins:hibernate5:7.3.0'
        classpath 'commons-io:commons-io:2.8.0'
        classpath 'org.grails.plugins:views-gradle:2.3.2'
        classpath 'com.bmuschko:gradle-docker-plugin:6.0.0'
    }
}


plugins {
    id 'eclipse'
    id 'idea'
    id 'java'
    id 'java-library'
    id 'groovy'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}


version '0.1'
group 'rest.docs'

apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.plugins.views-json'

repositories {
    maven { url 'https://repo.grails.org/grails/core' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
        }
        if (details.requested.name == 'commons-logging') {
            details.useTarget "org.slf4j:jcl-over-slf4j:1.7.5"
        }
    }
}

springBoot {
    mainClass = 'rest.docs.Application'
}

dependencies {
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    developmentOnly 'io.methvin:directory-watcher:0.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.grails:grails-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.grails:grails-plugin-url-mappings'
    implementation 'org.grails:grails-plugin-rest'
    implementation 'org.grails:grails-plugin-codecs'
    implementation 'org.grails:grails-plugin-interceptors'
    implementation 'org.grails:grails-plugin-services'
    implementation 'org.grails:grails-plugin-datasource'
    implementation 'org.grails:grails-plugin-databinding'
    implementation 'org.grails:grails-web-boot'
    implementation 'org.grails:grails-logging'
    implementation 'org.grails:grails-plugin-i18n'
    implementation 'org.grails.plugins:cache'
    implementation 'org.grails.plugins:async'

    implementation 'org.grails.plugins:spring-security-rest:3.0.1'
//    implementation 'org.grails.plugins:spring-security-core:5.3.0'
    implementation 'org.grails.plugins:events'
    implementation 'org.grails.plugins:hibernate5'
    implementation 'org.hibernate:hibernate-ehcache:5.6.11.Final'
    implementation 'org.grails.plugins:views-json'
    implementation 'org.grails.plugins:views-json-templates'

    implementation 'io.github.virtualdogbert:logback-groovy-config:1.12.5'

    compileOnly 'io.micronaut:micronaut-inject-groovy'

    console 'org.grails:grails-console'
    profile 'org.grails.profiles:rest-api'

    runtimeOnly 'org.glassfish.web:el-impl:2.1.2-b03'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.apache.tomcat:tomcat-jdbc'
    runtimeOnly 'javax.xml.bind:jaxb-api:2.3.1'

    runtimeOnly "org.postgresql:postgresql:42.2.0"

    // for dev mode:
    developmentOnly "org.testcontainers:postgresql:1.15.1"
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

// Customize Git properties plugin.
gitProperties {
    // Change date format in git.properties file.
    dateFormat = "yyyy-MM-dd HH:mm:ssZ"
    dateFormatTimeZone = 'EST'
}

ext {
    dockerTag = "${project.name}:${project.version}".toLowerCase()
    dockerBuildDir = mkdir("${buildDir}/docker")
}
task prepareDocker(type: Copy, dependsOn: assemble) {
    description = 'Copy files from src/main/docker and application jar to Docker temporal build directory'
    group = 'Docker'

    from 'src/main/docker'
    from project.jar

    into dockerBuildDir
}
task createDockerfile(type: Dockerfile, dependsOn: prepareDocker) {
    description = 'Create a Dockerfile file'
    group = 'Docker'

    destFile = project.file("${dockerBuildDir}/Dockerfile")

    from 'openjdk:8u151-jdk-alpine'

    exposePort 8080

    workingDir '/app'

    copyFile jar.archiveName, 'rest-docs.jar'
    copyFile 'app-entrypoint.sh', 'app-entrypoint.sh'
    runCommand 'chmod +x app-entrypoint.sh'

    entryPoint '/app/app-entrypoint.sh'
}
task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    description = 'Create Docker image to run the Grails application'
    group = 'Docker'

    inputDir = file(dockerBuildDir)
    images.add(dockerTag)
}


apply from: 'gradle/restdocs.gradle'